#!/usr/bin/perl
#
# gbAlignIServerCopy [options] workdir
#
# Copy the work directory to the iservers using rsync.
#
# Options:
#   -clusterRootDir=dir - The directory where the files will exist on the
#    cluster. They will be rsynced to this location on the iserver.  The
#    lowest two levels of the workdir are included, resulting, e.g.:
#    /dir/work/align.  Overrides cluster.rootDir conf item.
#
#   -iserver=host - explictly specify an iservers to use.  Maybe repeated.
#    If none are specified, the servers are obtained from the cluster.iserver
#
#   -verbose=n - print details
#
# Arguments:
#   o workdir - directory to sync, the last two levels are the directories
#     to sync to the server rest/work2/work1 gets synced to
#     iserverdir/work2/work1, with old directories under work2 being deleted.
#
# $Id: gbAlignIServerCopy,v 1.3 2003/07/29 04:44:49 markd Exp $
#
use strict;
use warnings;
use File::Basename;
use FindBin;
use lib "$FindBin::Bin/../lib";
use gbCommon;

my $clusterRootDir;

# Start a rsync process, store pid in %main::pidTable
sub startRSync($$) {
    my($iserver, $workdir) = @_;
    my @cmd = ("gbAlignRSync", $iserver, $workdir, $clusterRootDir);
    backgroundStart("$iserver rsync", @cmd);
}

# Entry
setTaskName("gbAlignIServerCopy");

# parse into globals used in program
$main::cwd = `pwd`;
chomp($main::cwd);

my @iservers;
while (($#ARGV >= 0) && ($ARGV[0] =~/^-.*/)) {
    my $opt = $ARGV[0];
    shift @ARGV;
    if ($opt =~ /^-clusterRootDir($|=)/) {
        $clusterRootDir = parseOptEq($opt);
    } elsif ($opt =~ /^-iserver($|=)/) {
        push(@iservers, parseOptEq($opt));
    } elsif ($opt eq "-verbose") {
        $gbCommon::verbose = 1;
    } elsif ($opt =~ /^-verbose=/) {
        $gbCommon::verbose = parseOptEq($opt);
    } else {
        gbError("invalid option \"$opt\"");
    }
}
my $workdir = $ARGV[0];

if ($#ARGV != 0) {
    gbError("wrong # args: gbAlignIServerCopy [options] workdir");
}
if ($#iservers < 0) {
    @iservers = splitSpaceList(getConfNo("cluster.iservers"));
}
if (!defined($clusterRootDir)) {
    $clusterRootDir = getConf("cluster.rootBir");
}

if ($gbCommon::verbose) {
    prMsg("start rsync processes");
}
foreach my $iserver (@iservers) {
    startRSync($iserver, $workdir);
}
if ($gbCommon::verbose) {
    prMsg("wait for rsync processes");
}
while (backgroundWait() > 0) {
}
   
if ($gbCommon::verbose) {
    prMsg("rsync complete");
}
