#!/usr/bin/perl
#
# gbAlignIServerCopy [options] workdir iserverdir
#
# Copy the work directory to the iservers using rsync.
#
# Options:
#   -omit-iserver=host - Specify an iserver to omit from the default list.
#    use if an iserver is down and remapped.
#   -iserver=host - Explictly specify an iserver to use.  Maybe repeated.
#    If none are specified, the default list is used.
#   -verbose=n - print details
#
# Arguments:
#   o workdir - directory to sync, the last two levels are the directories
#     to sync to the server rest/work2/work1 gets synced to
#     iserverdir/work2/work1, with old directories under work2 being deleted.
#
#   o iserverdir - directory on iservers
#
#
use strict;
use warnings;
use File::Basename;
use FindBin;
use lib "$FindBin::Bin/../lib";
use gbCommon;

# Start a rsync process, store pid in %main::pidTable
sub startRSync($$$) {
    my($iserver, $workdir, $iserverdir) = @_;
    my @cmd = ("gbAlignRSync", $iserver, $workdir, $iserverdir);
    backgroundStart("$iserver rsync", @cmd);
}

# Entry
setTaskName("gbAlignIServerCopy");

# parse into globals used in program
$main::cwd = `pwd`;
chomp($main::cwd);

my %omitIServers;
my @iservers;
while (($#ARGV >= 0) && ($ARGV[0] =~/^-.*/)) {
    my $opt = $ARGV[0];
    shift @ARGV;
    if ($opt =~ /^-omit-iserver($|=)/) {
        my $omit = parseOptEq($opt);
        $omitIServers{$omit} = 1;
    } elsif ($opt =~ /^-iserver($|=)/) {
        push(@iservers, parseOptEq($opt));
    } elsif ($opt eq "-verbose") {
        $gbCommon::verbose = 1;
    } elsif ($opt =~ /^-verbose=/) {
        $gbCommon::verbose = parseOptEq($opt);
    } else {
        gbError("invalid option \"$opt\"");
    }
}
if ($#ARGV != 1) {
    gbError("wrong # args: gbAlignIServerCopy [options] workdir iserverdir");
}
if ($#iservers < 0) {
    @iservers = @gbCommon::ISERVERS;
}

my $workdir = $ARGV[0];
my $iserverdir = $ARGV[1];

if ($gbCommon::verbose) {
    prMsg("start rsync processes");
}
foreach my $iserver (@iservers) {
    if (!defined($omitIServers{$iserver})) {
        startRSync($iserver, $workdir, $iserverdir);
    }
}
if ($gbCommon::verbose) {
    prMsg("wait for rsync processes");
}
while (backgroundWait() > 0) {
}
   
if ($gbCommon::verbose) {
    prMsg("rsync complete");
}
