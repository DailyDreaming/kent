#!/usr/bin/awk -f
#
# Convert NCBI createKeys.sql (which maybe sybase) to mysql.
#
{
    $0 = gensub("\\[dbo\\]\\.", "", "g", $0);
    $0 = gensub("\\[([^\\[]+)\\]", "\\1", "g", $0);
}

# get keys from sql commands like
#  ALTER TABLE Accessions ADD 

($1=="ALTER") && ($2=="TABLE") && ($4=="ADD") {
    # beginning of new table
    table = $3;
    inAlter = 1;
}

inConstr && /REFERENCES/ {
    # end of constraints
    inConstr = 0;
}

inConstr && /[a-zA-Z_]+/ {
    field = $1;
    if (!startedAlter) {
        printf "ALTER TABLE %s ADD INDEX (%s)", table, field;
        startedAlter = 1
    } else {
        printf ", ADD INDEX (%s)", field;
    }
}

inAlter && /CONSTRAINT/ {
    # beginning of constraints
    inConstr = 1;
}

($1=="GO") {
    inAlter = 0;
    if (startedAlter) {
        printf ";\n";
    }
    startedAlter = 0;
}
