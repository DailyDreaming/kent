#!/usr/bin/env perl

# File: findAccession
# Author: Terry Furey
# Date: 5/17/01
# Project: Human
# Description:  Finds the accession for a marker on the golden path

# $Id: findAccession,v 1.2 2006/09/09 21:50:58 hiram Exp $

use strict;
use warnings;

# Usage message
if ($#ARGV < 1) {
  print STDERR "USAGE: findAccession [-agp -ncbi -mouse -rat -chicken -all] <file> <accession_info>\n";
  exit(1);
}

# Read parameters
my $agp = 0;
my $ncbi = 0;
my $mouse = 0;
my $rat = 0;
my $chicken = 0;
my $all = 0;
my @chroms = (1..22,'X','Y','M','NA','UL');
while ($#ARGV > 1) {
  my $arg = shift(@ARGV);
  if ($arg eq "-agp") {
    $agp = 1;
 } elsif ($arg eq "-ncbi") {
    $ncbi = 1;
    @chroms = (1..22,'X','Y','M','Un');
  } elsif ($arg eq "-mouse") {
    $mouse = 1;
    @chroms = (1..19,'X','Y','Un','M');
  } elsif ($arg eq "-rat") {
    $rat = 1;
    @chroms = (1..20,'X','Un','M');
  } elsif ($arg eq '-chicken') {
    $chicken = 1;
    @chroms = (1..24,26..28,32,'E22C19W28','E26C13','E50C23','W','Z','Un');
  } elsif ($arg eq "-all") {
    $all = 1;
  } else {
    print STDERR "$arg is not a valid parameter\n";
  }
}
my $file = shift(@ARGV);
my $inFile = $file;
my $info = shift(@ARGV);
open(FILE, "<$file") || die("Can not open $file: $!");
open(OUT, ">$file.acc") || die "Can not open > $file.acc: $!";

# Get chromosomes from agp directory, if possible
if (($agp) && (-e "$info/chrom_names")) {
    @chroms = split("\n", `cat $info/chrom_names`);
}

# Read in the positions of the accessions
print STDERR "Reading in accession_info\n";
# Either read from accession_info.rdb file
my @acc;
my $acc;
my @chr;
my @start;
my @end;

my $index = 0;

if (!$agp) {
  if (! -e $info) { die("$info does not exist\n"); }
  `sorttbl Chr -r Ord Start < $info > $$.sort`;
  open(INFO, "<$$.sort") || die("Count not open $$.sort\n");
  my $line = <INFO>;
  $line = <INFO>;
  while ($line = <INFO>) {
    chomp($line);
    my ($chr, $ord, $start, $end, $mid, $ctg, $ctgIdx, $bargeIdx, $clone, $acc, @rest) = split("\t",$line);
    $acc[$index] = $acc;
    if ($ord == 1) {
      $chr[$index] = $chr;
    } else {
      $chr[$index] = "${chr}_random";
    }
    $start[$index] = $start;
    $end[$index] = $end;
    $index++;
  }
  close(INFO);
# Or read directly from agp file
} else {
  $index = -1;
  foreach my $chr (@chroms) {
    for (my $ord = 0; $ord <=1; $ord++) {
      if ($ord == 0) {
	$file = "chr${chr}_random.agp";
      } else {
	$file = "chr$chr.agp";
      }
	  if (-e "$info/$chr/$file") {
	    if (open(AGP, "<$info/$chr/$file")) {
	    print STDERR "Reading chr$chr agp info $info/$chr/$file\n";
	    my $prevacc = "";
	    while (my $line = <AGP>) {
	      chomp($line);
	      my ($thischr, $start, $end, $idx, $type, $acc, @rest) = split(' ',$line);
	      next if ($type eq "N");

	      if (! $chicken) {
		  ($acc, my $vers) = split(/\./,$acc);
	      }
	      if ($acc ne $prevacc) {
		$index++;
		$acc[$index] = $acc;
		if ($ord == 1) {
		  $chr[$index] = "chr$chr";
		} else {
		  $chr[$index] = "chr${chr}_random";
		}
		$start[$index] = $start;
		$end[$index] = $end;
	      } else {
		$end[$index] = $end;
	      }
	      $prevacc = $acc;
	    }
	    close(AGP);
	  } else {
	    print STDERR ("Could not open $info/$chr/$file\n");
	  }
      }
    }
  }
  $index++;
}

# Process each line in the position file
my $i = 0;
my $count = 0;
my $firsti = 0;
my $largest = 0;
my $largesti = 0;
print STDERR "processing $inFile\n";
while(my $line = <FILE>) {
  chomp($line);
  my ($chr, $start, $end, @rest) = split(' ',$line);
  $count++;
  if ($count%10000 == 0) {
    print STDERR "$count\n";
  }

  # Make sure at current chromosome
  if ($chr ne $chr[$i]) {
    $i = 0;
    while ($chr[$i] ne $chr) {
      $i++;
      if ($i > $#chr) {
	die("Can't find chrom $chr in \@chr (\$i=$i)\n");
      }
    }
    $firsti = $i;
    $largest = $end[$i];
    $largesti = $i;
  }

  # Find a range that overlaps the placement
  $acc = "";
  $i = $largesti;
  my $reset = 0;
  # If want all accessions, make sure start with first accession that overlaps
  if ($all) {
    while (($end[$i] > $start) && ($i > $firsti)) {
      $i--;
    }
    while (($start[$i] <= $end) && ($chr[$i] eq $chr)) {
      if (($start[$i] <= $end) && ($end[$i] >= $start)) {
	if ($acc) {
	  $acc .= ",$acc[$i]";
	} else {
	  $acc = $acc[$i];
	}
      }
      $i++;
    }
  # If want only best accession that overlaps
  } else {
    while (!$acc) {
      # Check if completely contained
      if (($start >= $start[$i]) && ($end <= $end[$i])) {
	# Check if this is a very long accession and is contained in next accession
	if (($end[$i] - $start[$i]) >= 500000) {
	  my $diffi = $end[$i] - $start[$i];
	  my $j = $i + 1;
	  while (($start <= $end[$j]) && ($chr[$j] eq $chr)) {
	    if (($start >= $start[$j]) && ($end <= $end[$j]) && (($end[$j] - $start[$j]) < $diffi)) {
	      $i = $j;
	      $diffi = $end[$j] - $start[$j];
	      if ($end[$j] > $largest) {
		$largest = $end[$j];
		$largesti = $j;
	      }
	    }
	    $j++;
	  }
	}
	$acc = $acc[$i];
	if ($chr[$i] ne $chr) {
	  die("Doesn't work - $chr $start $end\n");
	}
      # Check if out of range for chromosome
      } elsif (($start >= $largest)) {
	$i++;
	if ($end[$i] > $largest) {
	  $largest = $end[$i];
	  $largesti = $i;
	}
	if ($chr ne $chr[$i]) {
	  die("1 - Out of chrom - $chr $start $end - $chr[$i] $start[$i] $end[$i] $i line: $.\n");
	}
      # Check if accession really does contain feature
      } elsif ($start[$i] > $end)  {
	$i--;
	if ($i < $largesti) {
	  if ($reset) {
	    die("Doesn't line up - $chr $start $end - $chr[$i] $start[$i] $end[$i] $i\n");
	  }
	  $reset = 1;
	  $i = $firsti;
	  $largest = $end[$i];
	  $largesti = $i;	
	}
	if ($chr ne $chr[$i]) {
	  die("2 - Out of chrom - $chr $start $end - $chr[$i] $start[$i] $end[$i] $i\n");
	}

      # If feature rrosses the border of more than one accession - find the largest overlap
      } else {
	while (($end[$i] > $start) && ($chr eq $chr[$i]) && ($i >= 0)) {
	  $i--;
	} 
	$i++;
	# Find largest overlap
	my $overlap = 0;
	$acc = "";
	while (($start[$i] < $end) && ($chr eq $chr[$i]) && ($i < $index)) {
	  my $test = $end - $start + 1;
	  if ($start < $start[$i]) {
	    $test -= ($start[$i] - $start);
	  }
	  if ($end > $end[$i]) {
	    $test -= ($end - $end[$i]);
	  }
	  if ($test > $overlap) {
	    $overlap = $test;
	    $acc = $acc[$i];
	  }
	  $i++;
	}
	if ($chr ne $chr[$i]) {
	  $i--;
	}
      }
    }
  }
  # Print it out
  print OUT "$line\t$acc\n";
}
close(FILE);
close(OUT);
if (!$agp) {
  `rm $$.*`;
}
