#!/usr/bin/awk -f


# use the Examining stderr message to find dir name, however means
# error messages are consumed.  So try to find errors let them pass
# through.

BEGIN {
    usage="cvsStatusFilter -h\n\n"\
        "Filter the output of cvs status and extract information\n"\
        "in a parsable format. This requries both stdout and stderr\n"\
        "from cvs status. Output format is\n"\
        "   id  status\n"\
        "Options:\n"\
        "  -h - print this help message and exit\n"

    i = 1;
    while ((i <= ARGC) && (ARGV[i] ~ /^-.*/)) {
        if (ARGV[i] == "-h") {
            print usage > "/dev/stderr";
            exit 1;
        } else {
            print "Error: unknown option", ARGV[i];
            print usage > "/dev/stderr";
            exit 1;
        }
    }
    OFS="\t";
    dir=".";

}
/status aborted/ {
    print $0 > "/dev/stderr";
    exit 1
}
/cvs status: Examining/ {
    dir=$4;
}
/cvs server: Examining/ {
    dir=$4;
}

# File: cvs-stat         	Status: Locally Added
# File: no file cvs-modified		Status: Entry Invalid
/File:.*Status:/ {
    if ($0 ~ /File: no file/) {
        fname = $4;
        stIdx = 6;
    } else {
        fname = $2;
        stIdx = 4;
    }
    stat = $stIdx;
    for (i = stIdx+1; i <= NF; i++) {
        stat = stat "_" $i;
    }
    if (dir == ".") {
        print stat, fname;
    } else {
        print stat, dir "/" fname;
    }
}
