// A simple ascii game of hangman.

include lib

array of string words = 
   (
   "spider",
   "investigate",
   "wonder",
   "library",
   "flowers",
   "muffin",
   "beetle",
   "muffin",
   "television",
   "refrigerator",
   "waterfall",
   "museum",
   "butterfly",
   "juice",
   "computer",
   "picture",
   "astronomy",
   );

/* Pick our word randomly from list. */
randInit();
string word = words[lib.randomIx(words.size)];

/* Print out instructions. */
print("Let's play hangman. You've got 13 letters:");

/* Keep track of the parts of word we've guessed and # of guesses. */
array[word.size] of bit guessed;
int guessCount = 0;
string guesses = "";

/* The main game loop.   */
for (;;)
   {
   /* Create a string that is dashes where we haven't guessed the
    * word yet. */
   string s = word.dupe();
   for (int i=0; i<word.size; i += 1)
       {
       if (!guessed[i])
	   s[i] = '-';
       }

   /* Print out guess count and what we know of word. */
   print(guessCount + " " + s + " (" + guesses + ")");

   /* Wait for keypress, and convert key to lower case. */
   string key = keyIn();
   key.lower();
   guesses += key;

   /* Mark spot on words that use this letter, and count
    * up all the letters in the word that we know. */
   int goodLetters = 0;
   for (int i=0; i<word.size; i += 1)
       {
       if (word[i] == key[0])
	  guessed[i] = 1;
       if (guessed[i])
          goodLetters += 1;
       }

   /* Hey, we've got all the letters, we're done. */
   if (goodLetters == word.size)
       {
       print(word + "!!!  You won.");
       break;
       }

    /* See if we've run out of guesses. */
    guessCount += 1;
    if (guessCount > 13)
	{
        print("Sorry, after 13 guesses you are hung on " + word + "!");
	break;
	}
    }
